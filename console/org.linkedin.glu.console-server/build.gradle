/*
 * Copyright (c) 2010-2010 LinkedIn, Inc
 * Portions Copyright (c) 2011-2013 Yan Pujante
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

import org.linkedin.gradle.tasks.SingleArtifactTask

apply plugin: 'org.linkedin.cmdline'

configurations {
  jetty
}

dependencies {
  jetty spec.external.jettyPackage
}

def jettyDistribution = "jetty-distribution-${spec.versions.jetty}"
def jettyArchive = configurations.jetty.resolve().toList()[0]

def consoleWebappProject = evaluationDependsOn(':console:org.linkedin.glu.console-webapp')
def sampleWebappProject = evaluationDependsOn(':samples:org.linkedin.glu.samples.sample-webapp')
def gluDocumentationProject

if(project.properties['no.documentation'] != 'true')
{
  gluDocumentationProject = evaluationDependsOn(':docs:manual')
}

File consoleWebapp = consoleWebappProject.tasks.getByPath('package-war').artifactFile
File sampleWebapp = sampleWebappProject.tasks.getByPath('war').archivePath

def scriptProjects =
  rootProject.childProjects.scripts.childProjects.values().collect { evaluationDependsOn(it.path) }

cmdline {
  folders = []

  noPackageInstallTask = true

  dependsOn = [
    ':console:org.linkedin.glu.console-webapp:war',
    ':samples:org.linkedin.glu.samples.sample-webapp:war',
    *scriptProjects.collect { "${it.path}:scripts"}
  ]

    // documentation
  if(gluDocumentationProject)
  {
    dependsOn << ':docs:manual:doc'

    resources << [
      from: gluDocumentationProject.htmlDocDir,
      into: new File(assemblePackageFile, 'glu/docs/html'),
      replaceTokens: false
    ]
  }

  replacementTokens = [
    'version': version,
    'jetty.distribution': jettyDistribution,
    'jetty.archive': jettyArchive.name,
    'console.webapp': consoleWebapp.name,
    'sample.webapp': sampleWebapp.name
  ]

  // licenses
  resources << [ packageIncludes ]

  // jetty
  resources << [
    from: tarTree(jettyArchive),
    replaceTokens: false
  ]

  // exploded console
  resources << [
    from: zipTree(consoleWebapp),
    into: new File(assemblePackageFile, "glu/repository/exploded-wars/${consoleWebappProject.name}-${consoleWebappProject.version}"),
    replaceTokens: false
  ]

  // sample-webapp.war
  resources << [
    from: sampleWebapp,
    into: new File(assemblePackageFile, 'glu/repository/wars'),
    replaceTokens: false
  ]

  // scripts
  scriptProjects.each { scriptProject ->
    scriptProject.tasks.withType(SingleArtifactTask.class).each { t ->
      resources << [
        from: t.artifactFile,
        into: new File(assemblePackageFile, "glu/repository/scripts/${scriptProject.name}-${scriptProject.version}"),
        replaceTokens: false
      ]
    }
  }
}

project.'package-assemble'.doLast {
  File basePackage = project.convention.plugins.cmdline.assemblePackageFile

  // getting rid of the unnecessary javadoc that comes with jetty!
  delete new File(basePackage, "${jettyDistribution}/javadoc")
}
