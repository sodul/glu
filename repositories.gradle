/*
 * Copyright (c) 2010-2010 LinkedIn, Inc
 * Portions Copyright (c) 2011-2013 Yan Pujante
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

def topReleaseDir = new File(userConfig.top.release.dir ?: "${topBuildDir}/release").canonicalFile
def topPublishDir = new File(userConfig.top.publish.dir ?: "${topBuildDir}/publish").canonicalFile

/**
 * Repositories for build
 */
allRepositories.build = {
  // for libs included in this project
  maven {
    url "file://${rootDir}/local-maven-repo"
  }

  ivy {
    name = "build-release"
    url = topReleaseDir
    layout 'maven'
  }

  maven {
    name 'build-publish'
    url topPublishDir.toURI()
  }

  maven {
    name 'grails-central'
    url "http://repo.grails.org/grails/core"
  }

  mavenCentral()

  maven {
    name 'restlet'
    url "http://maven.restlet.org"
  }

  jcenter()
}

/**
 * Repositories for buildScript
 */
allRepositories.buildscript = allRepositories.build

/**
 * Repository where the zookeeper-cli and zookeeper-server are being published
 */
allRepositories.utilsZookeeperDistribution = {
  ivy {
    url = 'http://dl.bintray.com/content/pongasoft/utils-zookeeper'
    layout "pattern", {
      artifact "[artifact]-[revision](-[classifier])(.[ext])"
      ivy "[module]-[revision](-[classifier]).ivy"
    }
  }
}

/**
 * Repositories for release
 */
allRepositories.snapshotRelease = {
  ivy {
    name = "local-release"
    url = topReleaseDir
    layout 'maven'
  }
}

allRepositories.release = allRepositories.snapshotRelease

def pomConfig =  { pomToConfigure ->
  pomToConfigure.project {
    name project.rootProject.name
    description 'glu Deployment Automation Platform'
    url 'http://github.com/pongasoft/glu'
    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0'
      }
    }
    developers {
      developer {
        id 'ypujante'
        name 'Yan Pujante'
        email 'yan@pongasoft.com'
      }
    }
    scm {
      connection "scm:git:${spec.scmUrl}"
      developerConnection "scm:git:${spec.scmUrl}"
      url spec.scmUrl
    }
  }
}

/**
 * Repositories for publish
 */
allRepositories.snapshotPublish = {
  mavenDeployer {
    repository(url: "file://localhost${topPublishDir}")
    pom.whenConfigured(pomConfig)
  }
}

/**
 * Bintray binaries / maven style
 */
allRepositories.publish = bintray.binaries.mavenRepo {
  pom.whenConfigured(pomConfig)
}

/**
 * Distributions go to a different location
 */
allRepositories.distributions = bintray.distributions.ivyRepo()
